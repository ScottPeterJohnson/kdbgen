buildscript {
    ext.kotlin_version = '1.1.2'

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'com.jfrog.bintray' version '1.7.3'
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'maven-publish'


group 'net.justmachinery'
description = "Utility to generate Kotlin classes to interface with a database"
version = '0.3.2'

sourceCompatibility = 1.8

sourceSets {
    test.kotlin.srcDirs += "build/generated-sources/kotlin"
}

task generatePostgresInterface(type : JavaExec){
    classpath = sourceSets.test.compileClasspath
    main = "net.justmachinery.kdbgen.GeneratePostgresInterfaceKt"
    args '--databaseUrl=jdbc:postgresql://localhost:5432/kdbgentest?user=kdbgentest&password=kdbgentest'
    args '--enumPackage=net.justmachinery.kdbgen.test.generated.enums'
    args '--tablePackage=net.justmachinery.kdbgen.test.generated.tables'
}
compileTestKotlin.dependsOn('generatePostgresInterface')


repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile 'org.reflections:reflections:0.9.10'
    compile "org.postgresql:postgresql:9.4.1212"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile "com.xenomachina:kotlin-argparser:2.0.0"
    testCompile 'io.kotlintest:kotlintest:2.0.3'
}

task sourcesJar(type:Jar, dependsOn: classes){
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
            groupId 'net.justmachinery.kdbgen'
            artifactId 'kdbgen'
            version "$project.version"
            artifact sourcesJar
        }
    }
}

bintray {
    user = project.property("BINTRAY_USER")
    key = project.property("BINTRAY_KEY")
    pkg {
        repo = 'generic'
        name = 'kdbgen'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/ScottPeterJohnson/kdbgen.git'
        version {
            name = project.version
            desc = "$project.name version $project.version"
            released = new Date()
            vcsTag = "$project.version"
        }
    }
    publications = ['Publication']
}